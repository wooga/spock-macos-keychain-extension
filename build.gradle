/*
 * Copyright 2019 Wooga GmbH
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */
plugins {
    id 'java-library'
    id 'groovy'
    id 'maven-publish'
    id 'signing'
    id 'nebula.release' version '15.2.0'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.2'
    id 'io.codearte.nexus-staging' version '0.22.0'
    id "de.marcphilipp.nexus-publish" version "0.4.0"
}

group "com.wooga.spock.extensions"
List<String> cliTasks = project.rootProject.gradle.startParameter.taskNames
if (cliTasks.contains("rc")) {
    cliTasks.remove("rc")
    cliTasks.add("candidate")
    project.rootProject.gradle.startParameter.setTaskNames(cliTasks)
}

dependencies {
    implementation ('org.spockframework:spock-core:1.3-groovy-2.5')
    api 'com.wooga.security:macos-security:[1.1,2)'
}

repositories {
    mavenCentral()
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from javadoc
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        main(MavenPublication) {
            artifactId = project.name
            from(components["java"])
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            pom {
                name = 'Spock macOS keychain extension'
                description = 'A extension for Spock to create macos keychains during test.'
                url = 'https://github.com/wooga/spock-macos-keychain-extension'

                artifactId = project.name
                inceptionYear = "2019"

                scm {
                    connection = 'scm:git:https://github.com/wooga/spock-macos-keychain-extension.git'
                    developerConnection = 'scm:git:https://github.com/wooga/spock-macos-keychain-extension.git'
                    url = 'https://github.com/wooga/spock-macos-keychain-extension.git'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'manfred.endres'
                        name = 'Manfred Endres'
                        email = 'manfred.endres@wooga.net'
                    }
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype()
    }
}

nexusStaging {
    username = project.hasProperty('ossrhUsername') ? project.property('ossrhUsername') : System.getenv('OSSRH_USERNAME')
    password = project.hasProperty('ossrhPassword') ? project.property('ossrhPassword') : System.getenv('OSSRH_PASSWORD')
    packageGroup = "com.wooga"
}

signing {
    def signingKeyId = project.hasProperty("signingKeyId") ? project.property('signingKeyId') : System.getenv('OSSRH_SIGNING_KEY_ID')
    def signingKey = project.hasProperty("signingKey") ? project.property('signingKey') : System.getenv('OSSRH_SIGNING_KEY')
    def signingPassword = project.hasProperty('signingPassphrase') ? project.property('signingPassphrase') : System.getenv("OSSRH_SIGNING_PASSPHRASE")
    useInMemoryPgpKeys(signingKeyId.toString(), signingKey.toString(), signingPassword.toString())
    sign publishing.publications.main
}

postRelease.dependsOn(tasks.publish)
tasks."final".dependsOn(tasks.closeAndReleaseRepository)
tasks."candidate".dependsOn(tasks.closeAndReleaseRepository)
tasks.closeAndReleaseRepository.mustRunAfter(tasks.postRelease)
tasks.publish.mustRunAfter(tasks.release)
